#include "SPI.h"
#include <Adafruit_GFX.h>
#include <ILI9488.h>
#include <Adafruit_Sensor.h>
#include <DHT.h>

#define TFT_CS   8
#define TFT_DC   10
#define TFT_RST  9

#define DHTPIN 4        
#define DHTTYPE DHT11   
#define PUMP_PIN 2
#define MOISTURE_PIN A1

DHT dht(DHTPIN, DHTTYPE);
ILI9488 tft = ILI9488(TFT_CS, TFT_DC, TFT_RST);

float prevTemperature = -100; // Stores previous values
float prevHumidity = -100;
int prevMoisture = -1;

void setup() {
  Serial.begin(9600);
  dht.begin();
  pinMode(PUMP_PIN, OUTPUT);

  tft.begin();
  tft.setRotation(3); 
  tft.fillScreen(ILI9488_BLACK);

  // Initial Display Setup
  tft.setTextSize(3);
  
  tft.setCursor(30, 50);
  tft.setTextColor(ILI9488_WHITE);  
  tft.print("Temp: ");

  tft.setCursor(30, 100);
  tft.setTextColor(ILI9488_CYAN);
  tft.print("Humidity: ");

  tft.setCursor(30, 150);
  tft.setTextColor(ILI9488_GREEN);
  tft.print("Moisture: ");
}

void loop() {
  float temperature = dht.readTemperature();
  float humidity = dht.readHumidity();
  int moistureRaw = analogRead(MOISTURE_PIN);
  int moistureReal = map(moistureRaw, 1023, 0, 0, 100);

  Serial.print("Temperature: "); Serial.print(temperature); Serial.println("Â°C");
  Serial.print("Humidity: "); Serial.print(humidity); Serial.println("%");
  Serial.print("Soil Moisture: "); Serial.print(moistureReal); Serial.println("%");

  // Control Pump
  if (moistureReal < 55) { 
    digitalWrite(PUMP_PIN, LOW);
  } else {
    digitalWrite(PUMP_PIN, HIGH);
  }

  // Update display only if values have changed
  tft.setTextSize(3);
  
  if (temperature != prevTemperature) {
    tft.setCursor(180, 50);
    tft.setTextColor(ILI9488_WHITE, ILI9488_BLACK); // Overwrite previous text
    tft.print(temperature);
    tft.print(" C");
    prevTemperature = temperature;
  }

  if (humidity != prevHumidity) {
    tft.setCursor(220, 100);
    tft.setTextColor(ILI9488_CYAN, ILI9488_BLACK);
    tft.print(humidity);
    tft.print(" %");
    prevHumidity = humidity;
  }

  if (moistureReal != prevMoisture) {
    tft.setCursor(220, 150);
    tft.setTextColor(ILI9488_GREEN, ILI9488_BLACK);
    tft.print(moistureReal);
    tft.print(" %");
    prevMoisture = moistureReal;
  }

  delay(2000);
}
